apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.
apply plugin: 'app.cash.zipline'
apply plugin: 'app.cash.redwood.build.compose'

redwoodBuildCompose {
  targetCompilations = ['jsMain']
}

kotlin {
  apply from: "${rootDir}/addAllTargets.gradle"

  sourceSets {
    commonMain {
      dependencies {
        api projects.redwoodProtocol
        api libs.zipline
      }
    }

    jsMain {
      dependencies {
        api projects.redwoodProtocolCompose
        api libs.kotlinx.coroutines.core
        api libs.jetbrains.compose.runtime
      }
    }

    engineMain {
      dependsOn(commonMain)
      dependencies {
        api libs.zipline.loader
        api libs.kotlinx.coroutines.core
        api libs.kotlinx.serialization.json
        api libs.okio
        api projects.redwoodProtocolWidget
      }
    }
    nativeMain {
      dependsOn(engineMain)
    }
    androidMain {
      dependsOn(engineMain)
      dependencies {
        api libs.okHttp
      }
    }

    engineTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.kotlinx.coroutines.test
      }
    }
    nativeTest {
      dependsOn(engineTest)
    }
    androidTest {
      dependsOn(engineTest)
    }
  }
}

android {
  namespace 'app.cash.treehouse'
}
