apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.vanniktech.maven.publish'

dependencies {
  compileOnly gradleApi()
  compileOnly libs.kotlin.gradlePlugin
  compileOnly libs.androidGradlePlugin

  testImplementation libs.junit
  testImplementation libs.testParameterInjector
  testImplementation libs.truth
  testImplementation gradleTestKit()
}

gradlePlugin {
  plugins {
    redwood {
      id = "app.cash.redwood"
      displayName = "Redwood"
      description = "Redwood client Gradle plugin"
      implementationClass = "app.cash.redwood.gradle.RedwoodPlugin"
    }
    redwoodLint {
      id = "app.cash.redwood.lint"
      displayName = "Redwood lint"
      description = "Redwood lint Gradle plugin"
      implementationClass = "app.cash.redwood.gradle.RedwoodLintPlugin"
    }
    redwoodSchema {
      id = "app.cash.redwood.schema"
      displayName = "Redwood schema"
      description = "Redwood schema Gradle plugin"
      implementationClass = "app.cash.redwood.gradle.RedwoodSchemaPlugin"
    }
    redwoodSchemaCompose {
      id = "app.cash.redwood.schema.compose"
      displayName = "Redwood schema (Compose generator)"
      description = "Redwood schema Compose code generation Gradle plugin"
      implementationClass = "app.cash.redwood.gradle.RedwoodSchemaComposePlugin"
    }
    redwoodSchemaComposeProtocol {
      id = "app.cash.redwood.schema.compose.protocol"
      displayName = "Redwood schema (Compose protocolgenerator)"
      description = "Redwood schema Compose protocol code generation Gradle plugin"
      implementationClass = "app.cash.redwood.gradle.RedwoodSchemaComposeProtocolPlugin"
    }
    redwoodSchemaWidget {
      id = "app.cash.redwood.schema.widget"
      displayName = "Redwood schema (widget generator)"
      description = "Redwood schema widget code generation Gradle plugin"
      implementationClass = "app.cash.redwood.gradle.RedwoodSchemaWidgetPlugin"
    }
    redwoodSchemaWidgetProtocol {
      id = "app.cash.redwood.schema.widget.protocol"
      displayName = "Redwood schema (widget protocol generator)"
      description = "Redwood schema widget protocol code generation Gradle plugin"
      implementationClass = "app.cash.redwood.gradle.RedwoodSchemaWidgetProtocolPlugin"
    }
  }
}

test {
  dependsOn(':compose:runtime:installArchives')
  dependsOn(':redwood-cli:installArchives')
  dependsOn(':redwood-compose:installArchives')
  dependsOn(':redwood-gradle-plugin:installArchives')
  dependsOn(':redwood-lint:installArchives')
  dependsOn(':redwood-protocol:installArchives')
  dependsOn(':redwood-protocol-compose:installArchives')
  dependsOn(':redwood-protocol-widget:installArchives')
  dependsOn(':redwood-runtime:installArchives')
  dependsOn(':redwood-schema:installArchives')
  dependsOn(':redwood-schema-annotations:installArchives')
  dependsOn(':redwood-widget:installArchives')
}

def versionDirectory = "$buildDir/generated/version/"

sourceSets {
  main.java.srcDir versionDirectory
}

task pluginVersion {
  def outputDir = file(versionDirectory)

  inputs.property 'jbComposeVersion', libs.versions.jbCompose.get()
  inputs.property 'redwoodVersion', project.version
  outputs.dir outputDir

  doLast {
    def versionFile = file("$outputDir/app/cash/redwood/gradle/version.kt")
    versionFile.parentFile.mkdirs()
    versionFile.text = """// Generated file. Do not edit!
package app.cash.redwood.gradle

internal const val jbComposeVersion = "${libs.versions.jbCompose.get()}"
internal const val redwoodVersion = "${project.version}"
"""
  }
}

tasks.getByName('compileKotlin').dependsOn('pluginVersion')
