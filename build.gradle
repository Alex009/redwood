buildscript {
  ext.versions = [
    'kotlin': '1.4.10',
    'kotlinx': [
      'coroutines': '1.3.9',
      'serialization': '1.0.0',
    ],
    'ktor': '1.4.1',
    'exhaustive': '0.1.1',
    'compose': '1.0.0-alpha07',
  ]

  ext.deps = [
    'composeCompiler': "androidx.compose.compiler:compiler:${versions.compose}",
    'kotlin': [
      'reflect': 'org.jetbrains.kotlin:kotlin-reflect',
    ],
    'kotlinx': [
      'coroutines': [
        'android': "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinx.coroutines}",
        'core': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinx.coroutines}",
      ],
      'serialization': [
        'core': "org.jetbrains.kotlinx:kotlinx-serialization-core:${versions.kotlinx.serialization}",
        'json': "org.jetbrains.kotlinx:kotlinx-serialization-json:${versions.kotlinx.serialization}",
      ],
    ],
    'ktor': [
      'client': [
        'core': "io.ktor:ktor-client-core:${versions.ktor}",
      ],
      'server': [
        'core': "io.ktor:ktor-server-core:${versions.ktor}",
        'netty': "io.ktor:ktor-server-netty:${versions.ktor}",
        'webSockets': "io.ktor:ktor-websockets:${versions.ktor}",
      ],
    ],
    'logbackClassic': 'ch.qos.logback:logback-classic:1.2.3',
    'kotlinPoet': 'com.squareup:kotlinpoet:1.7.2',
    'clikt': 'com.github.ajalt.clikt:clikt:3.0.1',
    'exhaustiveAnnotation': "app.cash.exhaustive:exhaustive-annotation:${versions.exhaustive}",
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
    classpath 'com.android.tools.build:gradle:4.0.2'
    classpath "app.cash.exhaustive:exhaustive-gradle:${versions.exhaustive}"
  }
  repositories {
    mavenCentral()
    google()
    jcenter {
      // Required for a dependency of Android lint.
      content {
        includeGroup 'org.jetbrains.trove4j'
      }
    }
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    maven {
      url 'https://kotlin.bintray.com/kotlinx'
    }
    jcenter {
      // Required for a dependency of Android lint.
      content {
        includeGroup 'org.jetbrains.trove4j'
      }
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
    task.kotlinOptions {
      freeCompilerArgs += [
        '-progressive',
        '-Xopt-in=kotlin.RequiresOptIn',
        '-Xopt-in=androidx.compose.runtime.ExperimentalComposeApi',
      ]
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      useIR = true
    }
  }

  plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
    def android = plugin.extension
    android.compileSdkVersion 30
    android.defaultConfig {
      minSdkVersion 21
      targetSdkVersion 30
    }
    android.buildFeatures {
      aidl = false
      renderScript = false
      resValues = false
      shaders = false
    }
    android.lintOptions {
      checkDependencies true
      checkReleaseBuilds false // Full lint runs as part of 'build' task.
    }
  }

  def configureKotlinPlugin = {
    configurations {
      kotlinPlugin
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
      def pluginConfiguration = configurations.kotlinPlugin
      dependsOn(pluginConfiguration)
      doFirst {
        if (!pluginConfiguration.isEmpty()) {
          kotlinOptions{
            useIR = true
            freeCompilerArgs +=
              "-Xplugin=${pluginConfiguration.files.first()}"
          }
        }
      }
    }
  }
  plugins.withId('org.jetbrains.kotlin.android', configureKotlinPlugin)
  plugins.withId('org.jetbrains.kotlin.jvm', configureKotlinPlugin)
  plugins.withId('org.jetbrains.kotlin.multiplatform', configureKotlinPlugin)
}
