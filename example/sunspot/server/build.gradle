apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'

def generatedDir = 'build/generated/treehouse'

kotlin {
  android()
  jvm()

  sourceSets {
    commonMain {
      kotlin {
        srcDir generatedDir
      }
      dependencies {
        api project(':treehouse-server')
      }
    }
  }
}

configurations {
  generator
}

dependencies {
  kotlinPlugin deps.composeCompiler

  generator project(':treehouse-schema-generator')
  generator project(':example:sunspot')
}

def generate = tasks.register('treehouseGenerate', JavaExec) {
  it.classpath(configurations.generator)
  it.main('app.cash.treehouse.schema.generator.Main')
  it.args(
    '--server',
    '--out', generatedDir,
    'example.sunspot.Sunspot',
  )

  it.doFirst {
    file(generatedDir).deleteDir()
  }
}

kotlin.targets.all { target ->
  target.compilations.all { compilation ->
    compilation.compileKotlinTask.dependsOn(generate.get())
  }
}
