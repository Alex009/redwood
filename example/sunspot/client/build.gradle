apply plugin: 'org.jetbrains.kotlin.multiplatform'

archivesBaseName = 'sunspot-client'

kotlin {
  js {
    browser()
  }
  jvm()

  sourceSets {
    commonMain {
      dependencies {
        api project(':treehouse-client')
      }
    }
  }
}

configurations {
  generator
}

dependencies {
  generator project(':treehouse-schema-generator')
  generator project(':example:sunspot')
}

def generate = tasks.register('treehouseGenerate', JavaExec) {
  def outDir = 'src/commonMain/kotlin/'

  it.classpath(configurations.generator)
  it.main('app.cash.treehouse.schema.generator.Main')
  it.args(
    '--client',
    '--out', outDir,
    'example.sunspot.Sunspot',
  )

  it.doFirst {
    file(outDir).deleteDir()
  }
}

kotlin.targets.all { target ->
  target.compilations.all { compilation ->
    compilation.compileKotlinTask.dependsOn(generate.get())
  }
}
