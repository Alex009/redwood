import org.jetbrains.kotlin.gradle.plugin.KotlinPluginKt

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.

archivesBaseName = 'treehouse-jetpack-compose'

kotlin {
  android {
    publishAllLibraryVariants()
  }
  js {
    browser()
  }
  jvm()

  sourceSets {
    commonMain {
      kotlin {
        srcDir '../upstream/compose/runtime/runtime/src/commonMain/kotlin'
      }
      dependencies {
        api deps.kotlinx.coroutines.core
      }
    }
    commonTest {
      kotlin {
        // TODO get tests working on all platforms
        // srcDir '../upstream/compose/runtime/runtime/src/test/kotlin'
      }
      dependencies {
        implementation deps.kotlin.test
      }
    }
    androidMain {
      kotlin {
        srcDir '../upstream/compose/runtime/runtime/src/androidMain/kotlin'
        srcDir '../upstream/compose/runtime/runtime/src/jvmMain/kotlin'
      }
      dependencies {
        // Keep in sync with ../upstream/compose/runtime/runtime/build.gradle
        api 'androidx.annotation:annotation:1.1.0'
      }
    }
    jvmMain {
      kotlin {
        srcDir '../upstream/compose/runtime/runtime/src/jvmMain/kotlin'
        srcDir '../upstream/compose/runtime/runtime/src/desktopMain/kotlin'
      }
    }
    jvmTest {
      kotlin {
        srcDir '../upstream/compose/runtime/runtime/src/test/kotlin'
      }
      dependencies {
        implementation deps.kotlinx.coroutines.test
      }
    }
  }
}

android {
  sourceSets {
    main {
      manifest.srcFile '../upstream/compose/runtime/runtime/src/androidMain/AndroidManifest.xml'
    }
  }

  defaultConfig {
    consumerProguardFiles '../upstream/compose/runtime/runtime/proguard-rules.pro'
  }
}

dependencies {
  add(KotlinPluginKt.PLUGIN_CLASSPATH_CONFIGURATION_NAME, project(':compose:compiler'))
}
