import org.jetbrains.kotlin.gradle.plugin.KotlinPluginKt

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.

kotlin {
  android {
    publishAllLibraryVariants()
  }
  ios()
  js {
    browser()
  }
  jvm()

  sourceSets {
    commonMain {
      dependencies {
        api deps.kotlinx.coroutines.core
        api project(':treehouse-protocol')
        api project(':compose:runtime')
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test
        implementation project(':treehouse-test-schema:compose')
      }
    }
    androidMain {
      dependencies {
        implementation deps.androidx.core
      }
    }
    androidTest {
      dependsOn jvmTest
    }
    nativeTest {
    }
  }

  configure([targets.iosX64, targets.iosArm64]) {
    compilations.test.source(sourceSets.nativeTest)
  }
}

android {
  testOptions {
    unitTests.returnDefaultValues = true
  }
}

dependencies {
  add(KotlinPluginKt.PLUGIN_CLASSPATH_CONFIGURATION_NAME, project(':compose:compiler'))
  add(KotlinPluginKt.NATIVE_COMPILER_PLUGIN_CLASSPATH_CONFIGURATION_NAME, project(':compose:compiler-hosted'))
}

spotless {
  kotlin {
    targetExclude(
      // Apache 2-licensed files from AOSP.
      "src/androidMain/kotlin/app/cash/treehouse/compose/AndroidUiDispatcher.kt",
      "src/androidMain/kotlin/app/cash/treehouse/compose/AndroidUiFrameClock.kt",
    )
  }
}
