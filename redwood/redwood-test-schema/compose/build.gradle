apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'app.cash.redwood.tools.compose'

def generatedDir = 'build/generated/redwood'

kotlin {
  apply from: "${rootDir}/addAllTargets.gradle"

  sourceSets {
    commonMain {
      kotlin {
        srcDir generatedDir
      }
      dependencies {
        api projects.redwoodTestSchema.widget
        api projects.redwoodProtocolCompose // TODO should be redwoodCompose!
      }
    }
  }
}

configurations {
  generator
}

dependencies {
  generator projects.redwoodSchemaGenerator
  generator projects.redwoodTestSchema
}

def generate = tasks.register('redwoodGenerate', JavaExec) {
  it.classpath(configurations.generator)
  it.main('app.cash.redwood.schema.generator.Main')
  it.args(
    '--compose',
    '--out', generatedDir,
    'example.redwood.ExampleSchema',
  )

  it.doFirst {
    file(generatedDir).deleteDir()
  }
}

kotlin.targets.all { target ->
  target.compilations.all { compilation ->
    compilation.compileKotlinTask.dependsOn(generate.get())
  }
}
