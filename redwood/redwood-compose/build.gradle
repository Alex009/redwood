import org.jetbrains.kotlin.gradle.plugin.KotlinPluginKt

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.

kotlin {
  apply from: "${rootDir}/addAllTargets.gradle"

  sourceSets {
    commonMain {
      dependencies {
        api libs.kotlinx.coroutines.core
        api projects.redwoodProtocol // TODO Remove and create redwoodProtocolCompose!
        api projects.redwoodWidget
        api projects.compose.runtime
      }
    }
    commonTest {
      dependencies {
        implementation libs.kotlin.test
        implementation projects.redwoodTestSchema.compose
        implementation projects.redwoodTestSchema.compose.protocol
      }
    }
    androidMain {
      dependencies {
        implementation libs.androidx.core
      }
    }
    androidTest {
      dependsOn jvmTest
    }
  }
}

android {
  testOptions {
    unitTests.returnDefaultValues = true
  }
}

dependencies {
  add(KotlinPluginKt.PLUGIN_CLASSPATH_CONFIGURATION_NAME, libs.jetbrains.compose.compiler)
  add(KotlinPluginKt.NATIVE_COMPILER_PLUGIN_CLASSPATH_CONFIGURATION_NAME, libs.jetbrains.compose.compilerHosted)
}

spotless {
  kotlin {
    targetExclude(
      // Apache 2-licensed files from AOSP.
      "src/androidMain/kotlin/app/cash/redwood/compose/AndroidUiDispatcher.kt",
      "src/androidMain/kotlin/app/cash/redwood/compose/AndroidUiFrameClock.kt",
    )
  }
}
